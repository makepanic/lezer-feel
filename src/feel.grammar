@precedence {
  name,
  type,
  call,
  filter @left,
  qname @left,
  path @left,
  instanceOf,
  prefix,
  exp @left,
  times @left,
  add @left,
  compare @left,
  and @left,
  or @left,
  unaryTest @cut,
  then @left,
  else @left,
  paren
}

@top Expressions {
  expression0+
}

expression0[@dynamicPrecedence=-1] {
  expression ~semi (";" | insertSemi)?
}

expression[@isGroup=Expression] {
  textualExpression |
  boxedExpression
}

textualExpression {
  ForExpression |
  IfExpression |
  QuantifiedExpression |
  Disjunction |
  Conjunction |
  Comparison |
  ArithmeticExpression |
  InstanceOfExpression |
  PathExpression |
  FilterExpression |
  FunctionInvocation |
  literal |
  VariableName ~semi insertSemi? |
  kw<"?"> |
  SimplePositiveUnaryTest |
  ParenthesizedExpression
}

ForExpression {
  kw<"for"> InExpressions<IterationContext> ckw<"return"> expression
}

IfExpression {
  kw<"if"> expression !then kw<"then"> expression (!else kw<"else"> expression)?
}

QuantifiedExpression {
  (kw<"some"> | kw<"every">) InExpressions<expression> ckw<"satisfies"> expression
}

PathExpression {
  expression !path "." Name
}

FilterExpression {
  expression !filter "[" expression "]"
}

InstanceOfExpression {
  expression !instanceOf kw<"instance"> ckw<"of"> Type
}

ParenthesizedExpression {
  "(" expression !paren ")"
}

Disjunction {
  expression !or kw<"or"> expression
}

Conjunction [@dynamicPrecedence=-1] {
  expression !and kw<"and"> expression
}

boxedExpression {
  List |
  FunctionDefinition |
  Context
}

InExpressions<Expr> {
  commaSep1<InExpression<Expr>>
}

InExpression<Expr> {
  Name kw<"in"> Expr
}

IterationContext {
  expression (".." expression)?
}

ArithmeticExpression {
  expression !add (ArithOp<"+"> | minus) expression |
  expression !times (ArithOp<"*"> | divide) expression |
  expression !exp ArithOp<"^" | "**"> expression |
  !prefix (ArithOp<"+"> | minus) expression
}

Comparison {
  expression !compare CompareOp<"=" | "!="> expression |
  expression !compare CompareOp<">" | ">=" | "<" | "<="> expression |
  expression !compare kw<"between"> expression kw<"and"> expression |
  expression !compare kw<"in"> PositiveUnaryTest |
  expression !compare kw<"in"> "(" PositiveUnaryTests ")"
}

SimplePositiveUnaryTest {
  CompareOp<">" | ">=" | "<" | "<="> endpoint |
  Interval
}

@top UnaryTests {
  Wildcard { minus ~maybeNum } |
  PositiveUnaryTests |
  ckw<"not"> "(" PositiveUnaryTests ")"
}

PositiveUnaryTests {
  commaSep1<PositiveUnaryTest>
}

PositiveUnaryTest {
  expression
}

FunctionDefinition {
  kw<"function"> "(" FormalParameters ")" kw<"external">? FunctionBody { expression }
}

FormalParameters {
  commaSep<FormalParameter>
}

FormalParameter {
  ParameterName (":" Type)?
}

Type {
  QualifiedName |
  SpecialType |
  ListType { ckw<"list"> !type "<" Type !type ">" } |
  ContextType { ckw<"context"> !type "<" ContextEntryTypes !type ">" } |
  FunctionType { kw<"function"> !type "<" ArgumentTypes !type ">" "->" Type }
}

ArgumentTypes {
  commaSep<ArgumentType>
}

ArgumentType {
  Type
}

ContextEntryTypes {
  commaSep1<ContextEntryType>
}

ContextEntryType {
  Name ":" Type
}

Context {
  "{" commaSep<ContextEntry> "}"
}

ContextEntry {
  Key ":" expression
}

Key {
  Name |
  StringLiteral
}

DateTimeConstructor {
  ckw<"date"> |
  ckw<"date"> kw<"and"> ckw<"time"> |
  ckw<"time"> |
  ckw<"duration">
}

FunctionInvocation[@dynamicPrecedence=-1] {
  (SpecialFunctionName | expression) !call parameters
}

parameters {
  "(" (NamedParameters | PositionalParameters) ")"
}

NamedParameters {
  commaSep1<NamedParameter>
}

NamedParameter {
  ParameterName ":" expression
}

ParameterName {
  Name
}

PositionalParameters {
  commaSep<expression>
}

endpoint {
  QualifiedName |
  simpleLiteral ~literal
}

literal[@isGroup=Literal] {
  kw<"null"> |
  simpleLiteral ~literal
}

simpleLiteral {
  NumericLiteral |
  StringLiteral |
  BooleanLiteral |
  DateTimeLiteral
}

QualifiedName {
  Name (!qname "." Name)*
}

additionalIdentifier[@export] {
  !name identifier
}

VariableName {
  identifier additionalIdentifier* ~name
}

Name {
  identifier additionalIdentifier* ~name
}

Interval {
  ( "]" | "(" | "[" ) ~interval endpoint ".." endpoint ( ")" | "[" | "]" )
}

List {
  "[" ~interval commaSep<expression> "]"
}

SpecialType {
  ckw<"days"> kw<"and"> ckw<"time"> ckw<"duration"> |
  ckw<"years"> kw<"and"> ckw<"months"> ckw<"duration"> |
  ckw<"date"> kw<"and"> ckw<"time">
}

SpecialFunctionName {
  ckw<"years"> kw<"and"> ckw<"months"> ckw<"duration"> |
  ckw<"string"> ckw<"length"> |
  ckw<"upper"> ckw<"case"> |
  ckw<"lower"> ckw<"case"> |
  ckw<"substring"> ckw<"before"> |
  ckw<"substring"> ckw<"after"> |
  ckw<"starts"> ckw<"with"> |
  ckw<"ends"> ckw<"with"> |
  ckw<"list"> ckw<"contains"> |
  ckw<"insert"> ckw<"before"> |
  ckw<"index"> ckw<"of"> |
  ckw<"distinct"> ckw<"values"> |
  ckw<"met"> ckw<"by"> |
  ckw<"overlaps"> ckw<"before"> |
  ckw<"overlaps"> ckw<"after"> |
  ckw<"finished"> ckw<"by"> |
  ckw<"started"> ckw<"by"> |
  ckw<"day"> ckw<"of"> ckw<"year"> |
  ckw<"day"> ckw<"of"> ckw<"week"> |
  ckw<"month"> ckw<"of"> ckw<"year"> |
  ckw<"week"> ckw<"of"> ckw<"year"> |
  ckw<"get"> ckw<"value"> |
  ckw<"get"> ckw<"entries">
}

DateTimeLiteral {
  DateTimeConstructor parameters
}

NumericLiteral {
  minus? ~maybeNum number
}

StringLiteral {
  string
}

BooleanLiteral {
  @specialize<identifier, "true" | "false">
}

kw<Term> {
  @specialize[@name={Term}]<identifier, Term>
}

ckw<Term> {
  @extend[@name={Term}]<identifier, Term>
}

commaSep1<Expr> {
  Expr ("," Expr)*
}

commaSep<Expr> {
  "" | Expr ("," Expr)*
}

@external tokens insertSemicolon from "./tokens" {
  insertSemi
}

@skip { spaces | newline | LineComment | BlockComment }

@tokens {

  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }

  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment { "//" ![\n]* }

  BlockComment { "/*" blockCommentRest }

  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }

  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  digit {
    $[0-9]
  }

  digits {
    digit+
  }

  divide[@name=ArithOp] { "/" }

  minus[@name=ArithOp] { "-" }

  string {
    '"' (![\\\n"] | "\\" _)* '"'?
  }

  number {
    (digits ("." digits)? | "." digits)
  }

  @precedence { BlockComment, LineComment, divide }

  @precedence {
    number,
    "."
  }

  nameStart {
    nameStartChar namePartChar*
  }

  nameStartChar {
    "?" | $[A-Z] | "_" | $[a-z] |
    $[\u{200C}-\u{200D}] | $[$\u{2070}-\u{218F}] | $[\u{2C00}-\u{2FEF}] | $[\u{3001}-\u{D7FF}] | $[\u{F900}-\u{FDCF}] | $[\u{FDF0}-\u{FFFD}] | $[\u{10000}-\u{EFFFF}]
  }

  namePartChar {
    nameStartChar | digit | $[\u0300-\u036F] | $[\u203F-\u2040]
  }

  namePart {
    namePartChar+
  }

  identifier[@name=Identifier] {
    nameStart (namePart)*
  }

  @precedence {
    identifier,
    spaces
  }

  ">" "<" ">=" "<="

  "(" ")" "[" "]" "{" "}"

  ArithOp<Expr> {
    Expr
  }

  CompareOp<Expr> {
    Expr
  }

}

@detectDelim

@external propSource feelHighlighting from "./highlight"